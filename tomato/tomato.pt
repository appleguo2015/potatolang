// aPpLegUo
import "potato_ui.pt";
import "potato_file.pt";
import "./tomato/editor.pt";
import "./tomato/syntax_loader.pt";

// Tomato Main Entry

// Init Graphics Window
if (!ui_init_window(800, 600, "Tomato Editor")) {
  print "Failed to init graphics";
} else {

  let filename = "test.pt";
  let syntax_file = "tomato/potatolang.potato";

  print "Loading syntax from " + syntax_file + "...";
  let syntax_config = load_syntax(syntax_file);

  if (syntax_config == nil) {
    print "Failed to load syntax config.";
  } else {
    print "Syntax loaded.";
  }

  // Load File
  let lines = list();
  if (file_exists(filename)) {
      print "Loading " + filename + "...";
      let content = file_read(filename);
      if (content == nil) { content = ""; }
      
      // Split by newline
      let i = 0;
      let start = 0;
      let nl = char(10);
      let len_content = len(content);
      
      while (i < len_content) {
          if (char_at(content, i) == nl) {
              push(lines, substr(content, start, i - start));
              start = i + 1;
          }
          i = i + 1;
      }
      // Push last line if any
      if (start < len_content) {
          push(lines, substr(content, start, len_content - start));
      }
      
      if (len(lines) == 0) { push(lines, ""); } 
  } else {
      print "New file: " + filename;
      push(lines, "fun main() {");
      push(lines, "  print \"Hello Tomato\";");
      push(lines, "}");
  }

  let editor = create_editor(lines, syntax_config, filename);
  let running = true;

  while (running) {
    editor_draw(editor);
    ui_present();
    
    let key = get_key();
    while (key != nil) {
       if (key != "") {
          running = editor_handle_input(editor, key);
       }
       if (!running) { 
          key = nil; 
       } else { 
          key = get_key(); 
       }
    }
    
    sleep(10);
  }

  ui_clear();
  ui_draw_text(10, 10, "Bye!");
  ui_present();
  sleep(1000);
}
